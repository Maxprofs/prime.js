// Project info
group = "org.inversoft.prime"
version = "0.24"
description = "Prime.js Framework"

// Plugins
apply plugin: "java"
apply plugin: "idea"
apply plugin: "init"
apply plugin: "release"

// Build script config
buildscript {
  repositories {
    ivy {
      name "inversoftPublic"
      ivyPattern "http://hawking.inversoft.com/repository/public/[organisation]/[module]/[revision]/ivy.xml"
      artifactPattern "http://hawking.inversoft.com/repository/public/[organisation]/[module]/[revision]/[type]s/[artifact]-[revision].[ext]"
    }
    ivy {
      url "${project.gradle.gradleUserHomeDir}/integration-cache"
    }
  }
  dependencies {
    classpath(
      "org.primeframework.gradle:gradle-init-plugin:0.4",
      "org.primeframework.gradle:gradle-release-plugin:0.6"
    )
  }
}


configurations {
  js {
    description "JavaScript files"
    visible = true
    transitive = false
  }
}

upload {
  dependsOn uploadJs
}

artifacts {
  js(new File("${buildDir}/libs/prime.js")) {
    type "js"
  }
}

jar.doLast {
  ant.concat(destfile: "${buildDir}/libs/prime.js", eol: "lf") {
    fileset(dir: "src/main/js", includes: "*.js")
    fileset(dir: "src/main/js/sizzle", includes: "sizzle.js")
  }
}

task testJS << {
  def apacheHome = System.getenv("APACHE_HOME")
  if (apacheHome == null) {
    throw new GradleException("You must have an environment variable called APACHE_HOME that points to your apache web document root directory")
  }

  ant.symlink(link: "${apacheHome}/prime-tests", action: "delete")
  ant.symlink(link: "${apacheHome}/prime-tests", resource: "${projectDir}/src")
}
